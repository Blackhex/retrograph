buildscript {
  apply(from: "$rootDir/dependencies.gradle")
  apply(from: "$rootDir/scripts/deployment.gradle")

  repositories {
    google()
    mavenCentral()
    jcenter()
    kiwi(it)
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    classpath("org.jacoco:org.jacoco.core:$jacocoVersion")
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
  id "org.jetbrains.kotlin.kapt" version "$kotlinVersion"
  id "jacoco"
  id "maven-publish"
  id "com.jfrog.bintray" version "$bintrayPluginVersion"
}

repositories {
  google()
  mavenCentral()
  jcenter()
  kiwi(it)
  maven {
    setUrl("http://dl.bintray.com/jetbrains/spek")
  }
}

dependencies {
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion")
  implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
  implementation("com.squareup.retrofit2:retrofit:$retrofitVersion")
  implementation("com.squareup.retrofit2:converter-gson:$retrofitVersion")
  implementation("com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion")
  implementation("com.squareup.okhttp3:logging-interceptor:$okhttpVersion")
  implementation("com.google.code.gson:gson:$gsonVersion")
  implementation("io.reactivex.rxjava2:rxjava:$rxJavaVersion")

  testImplementation("com.squareup.okhttp3:mockwebserver:$okhttpVersion")

  testImplementation("junit:junit:$junitVersion")
  testImplementation("io.mockk:mockk:$mockkVersion")
  testImplementation("org.assertj:assertj-core:3.10.0")
  testImplementation("com.google.guava:guava:22.0")
}

// Test tasks are never up-to-date.
tasks.withType(Test) {
  outputs.upToDateWhen { false }
}

// region Code Coverage

jacoco {
  toolVersion = "$jacocoVersion"
}

jacocoTestReport {
  reports {
    xml.enabled = true
  }
}

tasks.withType(Test) {
  jacoco {
    includeNoLocationClasses = true
  }
}

// endregion Code Coverage

// region Deployment

publishLibrary("com.kiwi.mobile", "retrograph", retrographVersion)

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ["library"]

  pkg {
    repo = "Retrograph"
    name = "Retrograph"
    userOrg = "blackhex"
    vcsUrl = "https://github.com/SkyPicker/retrograph"
    licenses = ['Apache-2.0']
    version {
      name = retrographVersion
      desc = retrographVersion
      released  = new Date()
    }
  }
}

// endregion Deployment
